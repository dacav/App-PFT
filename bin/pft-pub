#!/usr/bin/perl
#
# Copyright 2014-2016 - Giovanni Simoni
#
# This file is part of PFT.
#
# PFT is free software: you can redistribute it and/or modify it under the
# terms of the GNU General Public License as published by the Free
# Software Foundation, either version 3 of the License, or (at your
# option) any later version.
#
# PFT is distributed in the hope that it will be useful, but WITHOUT ANY
# WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
# for more details.
#
# You should have received a copy of the GNU General Public License along
# with PFT.  If not, see <http://www.gnu.org/licenses/>.
#
=head1 NAME

pft pub - Publish content

=head1 SYNOPSYS

pft pub

=head1 DESCRIPTION

Publish content

=cut

use strict;
use warnings;
use utf8;

use feature qw/say state/;

use PFT::Tree;

use Pod::Usage;
use Pod::Find qw/pod_where/;

use File::Path qw/make_path remove_tree/;
use File::Copy::Recursive;

use Carp;

use Getopt::Long;
Getopt::Long::Configure 'bundling';

GetOptions(
    'help|h' => sub {
        pod2usage
            -exitval => 1,
            -verbose => 2,
            -input => App::PFT::help_of 'pub',
    }
) or exit 1;

my $tree = PFT::Tree->new;
my $conf = $tree->conf;
my $method = {
    'rsync+ssh' => \&rsync_ssh,
    'install'   => \&install,
}->{$conf->{remote}{method}};
die 'Unknown method ', $conf->{remote}{method} unless $method;

$method->($tree);

sub check {
    foreach (@_) {
        croak "Cannot use $conf->{remote}{$_}: ",
            "missing Remote.$_ in pft.yaml"
            unless defined $conf->{remote}{$_}
    }
}

sub rsync_ssh {
    my $tree = shift;

    check qw/user host path/;

    my $src = File::Spec->catfile($tree->dir_build, '');
    my $remote = $conf->{remote};
    my $dst = "$remote->{user}\@$remote->{host}:$remote->{path}";
    my $port = $remote->{port} || 22;

    local $, = "\n\t";
    say STDERR 'Sending with RSync', "from $src", "to $dst";

    system('rsync',
        '-e', "ssh -p $port",
        '--recursive',
        '--verbose',
        '--copy-links',
        '--times',
        '--delete',
        '--human-readable',
        '--progress',
        $src, $dst,
    );
}

sub install {
    my $tree = shift;
    check qw/path/;

    state $relstep = do {
        my $sup = File::Spec::Functions::updir;
        my $cur = File::Spec::Functions::curdir;
        qr/^(?:\Q$sup\E|\Q$cur\E)$/;
    };

    my $dst = (File::Spec->splitdir($conf->{path}))[0] =~ $relstep
        ? File::Spec->catdir($tree->basepath, $conf->{path})
        : $conf->{path}
        ;

    remove_tree $dst;
    make_path $dst, { verbose => 1 };

    local $File::Copy::Recursive::CopyLink = 0;
    File::Copy::Recursive::rcopy_glob(
        File::Spec->catfile($tree->dir_build, '*'),
        $dst,
    );
}
