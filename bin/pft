#!/usr/bin/perl

=head1 NAME

pft - Static blog and website manager

=head1 SYNOPSYS

pft <command> [options]

=head1 AVAILABLE COMMANDS

Here follows a list of available commands:

=over

=item *
B<blog> Create a new blog entry

=item *
B<page> Create a new page

=item *
B<build> Build the website

=back

For options of a specific command I<cmd> use C<pft I<cmd> --help>

=cut

use strict;
use warnings;

use File::Basename qw/basename/;
BEGIN { $0 = basename $0 }

use Pod::Usage;
use File::Spec::Functions qw/catfile/;

use App::PFT::Struct::Conf qw/$CONF_FILENAME cfg_default cfg_load cfg_dump/;
use App::PFT::Util qw/findroot/;

use App::PFT::Cmd::Init;
use App::PFT::Cmd::Blog;
use App::PFT::Cmd::Page;
use App::PFT::Cmd::Make;

use feature qw/say/;

my %COMMANDS = (
    blog => \&App::PFT::Cmd::Blog::main,
    page => \&App::PFT::Cmd::Page::main,
    init => \&App::PFT::Cmd::Init::main,
    make => \&App::PFT::Cmd::Make::main,
);

unless(@ARGV) {
    say 'Usage:';
    say "  $0 --help";
    say "  $0 $_ [options]" foreach keys %COMMANDS;
    exit 1;
}

my $command = shift @ARGV;

if ($command =~ /help|-h/) {
    pod2usage -exitval => 1, -verbose => 2;
}

if (my $root = findroot type=>'file', name=>$CONF_FILENAME) {
    if ($command eq 'init') {
        say STDERR "Already initialized in $root";
    }
    cfg_load $root;
    chdir $root;
} elsif ($command ne 'init') {
    say STDERR "Not a $0 site. Try running: $0 init";
    exit 1;
} else {
    cfg_default;
    cfg_dump '.';
}

my $call = $COMMANDS{$command} or die "Unknown command $command"; 

$call->();
